buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://raw.github.com/vRallev/mvn-repo/master/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'


sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
}

repositories {
    maven {
        url 'https://repository.apache.org'
    }
    mavenCentral()
}


dependencies {
    compile 'commons-codec:commons-codec:1.10'
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'io.reactivex:rxjava:1.0.3'
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.11'
}


task archiveSources(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task archiveJavadoc(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives shadowJar
    archives archiveSources
    archives archiveJavadoc
}


shadowJar {
    dependencies {
        include(dependency('org.apache.httpcomponents:httpclient:4.3.6'))
    }
    relocate 'org.apache', 'io.nextop.org.apache'
}


// properties defined in $HOME/gradle.properties
// @see http://central.sonatype.org/pages/gradle.html

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                groupId project.group
                artifactId project.name
                name 'Nextop Java Common'
                description 'Platform-agnostic Java core for Nextop'
                url 'http://nextop.io'
                scm {
                    url 'https://github.com/nextopio/nextop-client/tree/master/java-common'
                }
                developers {
                    developer {
                        name 'nextop.io'
                        email 'support@nextop.io'
                        organizationUrl 'http://nextop.io'
                    }
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
